PREFIX ex: <http://example.org/authors#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX wikibase: <http://wikiba.se/ontology#>
PREFIX bd: <http://www.bigdata.com/rdf#>
PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX schema: <http://schema.org/>

CONSTRUCT {
    ?author ?propertyID ?valueID .
    ?valueID rdfs:label ?valueLabelEs .
    ?valueID schema:description ?valueDescEs .
    ?propertyEntity rdfs:label ?propLabelEs .
}
WHERE {
    ?author a ex:author ;
        ex:wikidataId ?wikidataId .

    SERVICE <https://query.wikidata.org/sparql> {

        VALUES ?propertyID {
            wdt:P21   # Sexo o género
            wdt:P27   # País de nacionalidad
            wdt:P19   # Lugar de nacimiento
            wdt:P1412 # Lenguas habladas
        }

        ?wikidataId ?propertyID ?valueID .

        # Convert wdt:Pxxx -> wd:Pxxx to get the label of the property
        BIND(
          IRI(REPLACE(STR(?propertyID),
              "http://www.wikidata.org/prop/direct/",
              "http://www.wikidata.org/entity/"))
          AS ?propertyEntity
        )

        SERVICE wikibase:label {
            bd:serviceParam wikibase:language "es" .
            ?propertyEntity rdfs:label ?propLabelEs .
            ?valueID rdfs:label ?valueLabelEs .
            ?valueID schema:description ?valueDescEs .
        }
    }
}