PREFIX ex: <http://example.org/authors#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX wikibase: <http://wikiba.se/ontology#>
PREFIX bd: <http://www.bigdata.com/rdf#>
PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX schema: <http://schema.org/>

CONSTRUCT {
    ?author ?propertyID ?valueID .
    ?propertyEntity rdfs:label ?propLabelEs .
    ?valueID rdfs:label ?valueLabelEs .
}
WHERE {
    ?author a ex:author ;
        ex:wikidataId ?wikidataId .

    SERVICE <https://query.wikidata.org/sparql> {

        VALUES ?propertyID {
            wdt:P21
            wdt:P1477
            wdt:P2671
            wdt:P27
            schema:description
        }

        ?wikidataId ?propertyID ?valueID .

        FILTER(!STRSTARTS(STR(?valueID),
               "http://www.wikidata.org/entity/statement/"))

        # Convert wdt:Pxxx -> wd:Pxxx
        BIND(
          IRI(REPLACE(STR(?propertyID),
              "http://www.wikidata.org/prop/direct/",
              "http://www.wikidata.org/entity/"))
          AS ?propertyEntity
        )

        SERVICE wikibase:label {
            bd:serviceParam wikibase:language "es" .
            ?propertyEntity rdfs:label ?propLabelEs .
            ?valueID rdfs:label ?valueLabelEs .
        }
    }
}
